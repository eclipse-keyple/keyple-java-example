///////////////////////////////////////////////////////////////////////////////
//  GRADLE CONFIGURATION
///////////////////////////////////////////////////////////////////////////////
plugins {
    java
    id("com.diffplug.spotless") version "6.25.0"
    id("io.quarkus") version "1.8.1.Final"
}
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath("org.eclipse.keyple:keyple-gradle:0.2.+") { isChanging = true }
    }
}
apply(plugin = "org.eclipse.keyple")

///////////////////////////////////////////////////////////////////////////////
//  APP CONFIGURATION
///////////////////////////////////////////////////////////////////////////////
repositories {
    mavenLocal()
    mavenCentral()
    maven(url = "https://oss.sonatype.org/content/repositories/snapshots")
}

val javaSourceLevel: String by project
val javaTargetLevel: String by project
java {
    sourceCompatibility = JavaVersion.toVersion(javaSourceLevel)
    targetCompatibility = JavaVersion.toVersion(javaTargetLevel)
    println("Compiling Java $sourceCompatibility to Java $targetCompatibility.")
}

dependencies {
    /* Keyple dependencies */
// Begin Keyple configuration (generated by 'https://keyple.org/components/overview/configuration-wizard/')
    implementation("org.eclipse.keypop:keypop-reader-java-api:2.0.1-SNAPSHOT") { isChanging = true }
    implementation("org.eclipse.keypop:keypop-calypso-card-java-api:2.1.0-SNAPSHOT") { isChanging = true }
    implementation("org.eclipse.keyple:keyple-common-java-api:2.0.1-SNAPSHOT") { isChanging = true }
    implementation("org.eclipse.keyple:keyple-util-java-lib:2.3.2-SNAPSHOT") { isChanging = true }
    implementation("org.eclipse.keyple:keyple-service-java-lib:3.2.1-SNAPSHOT") { isChanging = true }
    implementation("org.eclipse.keyple:keyple-distributed-network-java-lib:2.3.1-SNAPSHOT") { isChanging = true }
    implementation("org.eclipse.keyple:keyple-distributed-local-java-lib:2.3.1-SNAPSHOT") { isChanging = true }
    implementation("org.eclipse.keyple:keyple-distributed-remote-java-lib:2.3.1-SNAPSHOT") { isChanging = true }
    implementation("org.eclipse.keyple:keyple-card-calypso-java-lib:3.1.1-SNAPSHOT") { isChanging = true }
    implementation("org.eclipse.keyple:keyple-plugin-pcsc-java-lib:2.2.1-SNAPSHOT") { isChanging = true }
    implementation("org.eclipse.keyple:keyple-plugin-stub-java-lib:2.2.1-SNAPSHOT") { isChanging = true }
// End Keyple configuration
    /* Quarkus */
    implementation(enforcedPlatform("io.quarkus:quarkus-universe-bom:1.8.1.Final"))
    implementation("io.quarkus:quarkus-resteasy-jsonb")
    implementation("io.quarkus:quarkus-resteasy")
    implementation("io.quarkus:quarkus-rest-client")
    testImplementation("io.quarkus:quarkus-junit5")
    testImplementation("io.rest-assured:rest-assured")
}

///////////////////////////////////////////////////////////////////////////////
//  TASKS CONFIGURATION
///////////////////////////////////////////////////////////////////////////////
tasks {
    spotless {
        java {
            target("**/src/**/*.java")
            licenseHeaderFile("${project.rootDir}/LICENSE_HEADER")
            importOrder("java", "javax", "org", "com", "")
            removeUnusedImports()
            googleJavaFormat()
        }
    }
}
val runExample by tasks.creating(Jar::class) {
    group = "keyple"
    dependsOn.add("quarkusDev")
}
